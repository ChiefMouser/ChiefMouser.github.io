---
layout: post
title: awk简介
date: 2016-03-04
category: CSE
tags: [awk]
---

about AWK

<!-- more -->

# 语法

[gawk](http://www.gnu.org/software/gawk/manual/gawk.html)

awk 是对输入进行逐行分析的，模式一旦匹配成功，就执行后面相应的action.

awk 是文本分析程序，虽然也有技巧可以使它做各种黑活，但是太tricky了，还不如来个python明白。

## 简单的

awk [-F] 'pattern {action}' file

```bash
awk '$3 >0 { print $1, $2 * $3 }' emp.data
cat /etc/passwd \| awk  -F ':'  '{print $1"\t"$7}'
awk '$3==0 && $6=="ESTABLE" \|\| NR==1 {printf "%02s %-20s %-20s\n",NR, $4,$5}' n.txt
awk '$3==0 && $6=="LISTEN" || NR==1 ' netstat.txt # 这个相当于过滤一条条记录
```

- -F 指定分割符，默认是空格，还可以指明多个比如 -F '[;:]''
- print 也可以改成完全C风格的printf
- 其中的“==”为比较运算符。其他比较运算符：!=, >, <, >=, <=

## 复杂的

awk ' BEGIN {...} {...}; END{...} '

```bash
awk -F ':' 'BEGIN {count=0;} {name[count] = $1;count++;}; END{for (i = 0; i < NR; i++) print i, name[i]}' /etc/passwd
awk  'BEGIN{FS=":"} {print $1,$3,$6}' /etc/passwd
```
```bash
#!/bin/awk -f
#运行前
BEGIN {
    math = 0
    english = 0
    computer = 0
 
    printf "NAME    NO.   MATH  ENGLISH  COMPUTER   TOTAL\n"
    printf "---------------------------------------------\n"
}
#运行中
{
    math+=$3
    english+=$4
    computer+=$5
    printf "%-6s %-6s %4d %8d %8d %8d\n", $1, $2, $3,$4,$5, $3+$4+$5
}
#运行后
END {
    printf "---------------------------------------------\n"
    printf "  TOTAL:%10d %8d %8d \n", math, english, computer
    printf "AVERAGE:%10.2f %8.2f %8.2f\n", math/NR, english/NR, computer/NR
}
```

- 可以将awk写成一个文件执行，调用时使用-f来指定：awk -f cal.awk score.txt

## 其他

还有条件控制等，实际上这几乎是一门编程语言。这里主要关注简单一些的状况


# 内建变量

$0      |   当前记录（这个变量中存放着整个行的内容）
$1~$n   |   当前记录的第n个字段，字段间由FS分隔
FS      |   输入字段分隔符 默认是空格或Tab
NF      |   当前记录中的字段个数，就是有多少列
NR      |   已经读出的记录数，就是行号，从1开始，如果有多个文件话，这个值也是不断累加中。
FNR     |   当前记录数，与NR不同的是，这个值会是各个文件自己的行号
RS      |   输入的记录分隔符， 默认为换行符
OFS     |   输出字段分隔符， 默认也是空格
ORS     |   输出的记录分隔符，默认为换行符
FILENAME|   当前输入文件的名字

# 匹配

可以在匹配部分使用grep类似的语法。模式以~开头，写在两个斜杠中间

```bash
awk '$6 ~ /FIN|TIME/ {print NR,$4,$5,$6}' 
```

表示第六个分量，如果是FIN或TIME就算做匹配成功

# 举例

主要来自leetcode

## 输出文件第十行

```bash
gawk 'NR==10 {print $0}' file.txt
gawk 'NR==10' file.txt
```

## [文件转置](https://leetcode.com/problems/transpose-file/)

```bash
awk ' { 
    for (i = 1; i <= NF; i++)  {
        a[NR, i] = $i
    }
}
NF > p { p = NF }
END {    
    for (j = 1; j <= p; j++) {
        str = a[1, j]
        for (i = 2; i <= NR; i++){
            str = str " " a[i, j]
        }
        print str
    }
}' file.txt
```

## [有效电话号码](https://leetcode.com/problems/valid-phone-numbers/)

```bash
awk  '$0 ~ /^\([0-9]{3}\) [0-9]{3}-[0-9]{4}$/ || /^[0-9]{3}-[0-9]{3}-[0-9]{4}$/ {print  $0}' file.txt
```

## [单词频率统计](https://leetcode.com/problems/word-frequency/)

```bash
awk '
BEGIN{
    while(getline < "words.txt") {
        for(i = 1; i <= NF; i += 1)  map_wf[$i]++; 
    }
    n = asort(map_wf, arr_f); 
    for(word in map_wf) {
        map_fw[map_wf[word]] = word; 
    }
    for(j = n; j >= 1; j -= 1) {
        f = arr_f[j];
        printf("%s %d\n", map_fw[f], f);
    }
}'
```